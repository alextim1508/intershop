plugins {
    id 'org.openapi.generator' version '7.12.0'
}

bootJar {
    archiveBaseName = 'intershop'
    archiveVersion = ''
}

dependencies {
    //========== Web ==========
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    //========== Security ==========
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.session:spring-session-data-redis'
    implementation 'org.springframework.security:spring-security-oauth2-client'

    //========== Database ==========
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    implementation "org.postgresql:r2dbc-postgresql:${postgresR2dbcVersion}"
    implementation 'org.liquibase:liquibase-core'
    implementation "org.postgresql:postgresql:${postgresVersion}"

    //========== Mapper ==========
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    //========== Utils ==========
    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor 'org.projectlombok:lombok'
    implementation("com.google.guava:guava:${guavaVersion}")

    //========== Test ==========
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'com.redis:testcontainers-redis:2.2.2'
    testImplementation 'org.testcontainers:r2dbc'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$projectDir/../open-api/api-spec.yml")
    outputDir.set("$projectDir/")
    ignoreFileOverride.set("$projectDir/../open-api/.openapi-generator-java-sources.ignore")
    apiPackage.set("com.alextim.intershop.client.pay.api")
    modelPackage.set("com.alextim.intershop.client.pay.dto")
    invokerPackage.set("com.alextim.intershop.client.pay.invoker")

    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true"
    ])
    serverVariables.set(['host': 'payment'])
}